#!/bin/tcsh
  foreach dir (sub-???????)
    cd $dir

    #set up QC directory structure
    mkdir QC

    foreach runname (meica*)
        set run = `echo $runname | tail -c 43`
        mkdir QC/$runname
        #check co-registration in AFNI
        #open AFNI
        afni -yesplugouts &
        sleep 5
        plugout_drive   \
        -com "OPEN_WINDOW A.sagittalimage mont=20x13" \
        -com "SWITCH_UNDERLAY ${dir}_anat_brain_do_at.nii.gz" \
        -com "SWITCH_OVERLAY ${run}_medn_afw.nii.gz" \
        -com "SAVE_JPEG A.sagittalimage Coreg_${run}.jpg" \
        -quit
        #close AFNI
        plugout_drive -com "QUIT" \
        -quit
        #move to QC directory
        mv Coreg* QC/$runname

        #copy files to corresponding QC directory
        cp $runname/TED/{feats_OC2.nii,accepted.txt,rejected.txt,comp_table.txt,hik_ts_OC.nii,lowk_ts_OC.nii} QC/$runname
        #put number of accepted, rejected, and total components for run into a single text file
        #visualize accepted components, inspect mefc file in fslview for each run/subjects ***MORE IMPORTANT TO DO THIS FOR SUBJ WITH <10 ACCEPTED COMPONENTS***
        cd QC/$runname
        awk -F '[\t,]' '{print $1, NF}' accepted.txt | awk '{print $2}' > accepted
        awk -F '[\t,]' '{print $1, NF}' rejected.txt | awk '{print $2}' > rejected
        awk -F '[\t,]' '{print $1, NF}' midk_rejected.txt | awk '{print $2}' > midkrejected
        grep "Total components generated by decomposition (TCo):" comp_table.txt | awk '{print $NF}' > total
        echo $dir > sumcomptable.txt && paste accepted rejected midkrejected total >> sumcomptable.txt #Accepted, Rejected, and Total components
        rm -r accepted* rejected* midkrejected*
        convert -monochrome -interword-spacing 30 text:sumcomptable.txt sumcomptable.jpg
        cd ../..

        #save rigid body motion parameter 1D files in corresponding QC directories as jpegs
        1dplot -volreg -plabel ${run} -jpeg QC/${runname}/${run}_motion ${runname}/motion.1D
        #get FD and DVARS estimates, graphical plots, and confound matrices
        cd $runname
        cp *echo-2*.nii ../QC/$runname
        fsl_motion_outliers -i *echo-2*.nii --dummy=4 --fd -s ../QC/$runname/${run}_FD.txt -p ../QC/$runname/${run}_FD.png -o ../QC/$runname/${run}_FDoutliers.txt
        fsl_motion_outliers -i *echo-2*.nii --dummy=4 --dvars -s ../QC/$runname/${run}_DVARS.txt -p ../QC/$runname/${run}_DVARS.png -o ../QC/$runname/${run}_DVARSoutliers.txt
        cd ..

        #display DVARS for hik and lowk components using mean of tsoc (tsoc included and medn included in plots to further observe change before and after denoising of optimal combination)
        #this part calls on dvars.py, make sure it exists in a known directory
        #first, copy tsoc and medn NAT files to QC folders (we'll get rid of these duplicates later)
        cp ${run}_medn_nat.nii.gz QC/$runname
        cp ${run}_tsoc_nat.nii.gz QC/$runname
        #second, cd into QC run directory and compute mean of each voxel time series for tsoc file
        cd QC/$runname
        3dTstat -mean -prefix ${run}_tsoc_nat_mean.nii.gz *tsoc_nat.nii.gz
        #next, add high k components to mean tsoc (high k file does not have the mean image in it; it is necessary to compute the mean image of tsoc and add it to high k); need to resample kappa files to tsoc (donâ€™t have bounding box)
        3dresample -master *tsoc_nat_mean.nii.gz -prefix rhik_ts_OC.nii.gz -inset hik_ts_OC.nii.gz
        3dcalc -a rhik_ts_OC.nii -b *tsoc_nat_mean.nii.gz -expr 'a+b' -prefix rhik_ts_OC_mean.nii.gz
        #same should be done for low k components
        3dresample -master *tsoc_nat_mean.nii.gz -prefix rlowk_ts_OC.nii.gz -inset lowk_ts_OC.nii.gz
        3dcalc -a rlowk_ts_OC.nii -b *tsoc_nat_mean.nii.gz -expr 'a+b' -prefix rlowk_ts_OC_mean.nii.gz
        #compute dvars on the newly calculated high k file including the mean of tsoc, low k file including the mean of tsoc, original tsoc file, and medn
        #change path to wherever script is
        python /home/lbcspreng/Desktop/derivatives/dvars.py rhik_ts_OC_mean.nii.gz
        python /home/lbcspreng/Desktop/derivatives/dvars.py rlowk_ts_OC_mean.nii.gz
        python /home/lbcspreng/Desktop/derivatives/dvars.py *tsoc_nat.nii.gz
        python /home/lbcspreng/Desktop/derivatives/dvars.py *_medn_nat.nii.gz

        #plot on same axes with labels
        1dRplot -input *hik*.1D *lowk*.1D *medn*.1D *tsoc*.1D -oneplot -col.color 1 2 3 4 -leg.show -leg.names hik lowk medn tsoc -leg.position top -title DVARS -xax.label TR -yax.label DVARS -save DVARScomparison

        #generate tSNR maps from medn files
        3dTstat -mean -prefix ${run}_medn_mean.nii.gz *medn_nat.nii.gz
        3dTstat -stdev -prefix ${run}_medn_sd.nii.gz *medn_nat.nii.gz
        3dcalc -a *medn_mean.nii.gz -b *medn_sd.nii.gz -expr 'a/b' -prefix ${run}_tsnr.nii.gz

        #save screenshots of atls and ofc in AFNI
        #open AFNI
        afni -yesplugouts &
        sleep 5
        plugout_drive   \
        -com "OPEN_WINDOW A.axialimage" \
        -com "OPEN_WINDOW A.sagittalimage" \
        -com "SWITCH_OVERLAY ${run}_tsnr.nii.gz"    \
        -com "SET_PBAR_SIGN A.+"    \
        -com "SET_DICOM_XYZ -37 -41 -44"   \
        -com "SAVE_JPEG A.sagittalimage atlsag" \
        -com "SAVE_JPEG A.axialimage atlaxial"  \
        -com "SET_DICOM_XYZ -2 -74 -22" \
        -com "SAVE_JPEG A.sagittalimage ofcsag" \
        -com "SAVE_JPEG A.axialimage ofcaxial"  \
        -quit
        #close AFNI
        plugout_drive -com "QUIT" \
        -quit

        #merge outputs into one report
        convert Coreg* sumcomptable.jpg *motion.jpg *FD.png *DVARS.png DVARScomparison* atl* ofc* ${runname}_FULLREPORT.pdf
        #remove copies of files (medn, tsoc, hik, lowk)
        rm -r *medn_nat.nii.gz *tsoc_nat.nii.gz hik_ts_OC.nii lowk_ts_OC.nii
        #cd back to sub directory
        cd ../..
        end
    #cd back to main directory
    cd ..
    end
